import org.apache.tools.ant.filters.ReplaceTokens

ext {
    publishedVersion = project.version
    bundlerScript = file('bin/build-package.sh')
    applicationLibraryPath = file("${rootProject.project('core').buildDir}/libraries")
    javafxModulePath = file("${rootProject.project('core').buildDir}/javafx-modules")
    mainJar = project(':core').jar.outputs.files[0].name
    bundlePath = file("${buildDir}/bundle")
    extraBundlerArguments = []
    if (operatingSystem.isMacOsX()) {
        packageType = 'pkg'
        appIcon = file('icons/mac/Verisimilitude.icns')
        sessionIcon = file('icons/mac/VerisimilitudeSession.icns')
        extraBundlerArguments = ['--mac-sign']
    } else if (operatingSystem.isLinux()) {
        packageType = 'deb'
        appIcon = file('icons/App512.png')
        sessionIcon = file('icons/Doc512.png')
    } else if (operatingSystem.isWindows()) {
        appIcon = file('icons/windows/Verisimilitude.ico')
        sessionIcon = file('icons/windows/VerisimilitudeSession.ico')
        bundlerScript = file('bin/build-package.bat')
        generatedInnoSetupFile = file("${buildDir}/resources/main/application.iss")
        windowsExecutable = file("${bundlePath}/Verisimilitude/Verisimilitude.exe")
        bundleContent = file("${bundlePath}/Verisimilitude/*")
    }
    if (publishedVersion.endsWith('-SNAPSHOT')) {
        publishedVersion = '0.0.1'
    }
}

task recreateIconMacApp(type: Exec) {
    description 'Regenerate the Mac application icon'
    commandLine 'sh', file('bin/mac-icon.sh'), file('icons/App512.png'), file('icons/mac'), 'Verisimilitude'
}

task recreateIconMacSession(type: Exec) {
    description 'Regenerate the Mac session file icon'
    commandLine 'sh', file('bin/mac-icon.sh'), file('icons/Doc512.png'), file('icons/mac'), 'VerisimilitudeSession'
}

task recreateIconWindowsApp(type: Exec) {
    description 'Regenerate the Windows application icon'
    commandLine 'sh', file('bin/windows-icon.sh'), file('icons/App512.png'), file('icons/windows'), 'Verisimilitude'
}

task recreateIconWindowsSession(type: Exec) {
    description 'Regenerate the Windows session file icon'
    commandLine 'sh', file('bin/windows-icon.sh'), file('icons/Doc512.png'), file('icons/windows'), 'VerisimilitudeSession'
}

task recreateIcons(dependsOn: [recreateIconMacApp, recreateIconMacSession, recreateIconWindowsApp, recreateIconWindowsSession]) {
    description 'Regenerate all the application bundle icons'
}

processResources {
    filesMatching('file-associations.properties') {
        filter ReplaceTokens, tokens: [
            'verisimilitude.session.icon': sessionIcon.toString()
        ]
    }
}

task checkBundlingEnvironment {
    description 'Check the environment for building the installable bundle'
    doLast {
        if (operatingSystem.isWindows()) {
            def javaHome = System.env.JAVA_HOME

            if (javaHome) {
                def javaExecutable = file("${javaHome}/bin/java.exe")
                def packagerFile = file("${javaHome}/bin/jpackager.exe")
                def jmodFile = file("${javaHome}/jmods/jdk.packager.jar")

                if (!javaExecutable.exists()) {
                    throw new GradleException("Unable to find Java executable '${javaExecutable}'")
                }
                if (!packagerFile.exists()) {
                    throw new GradleException("Unable to find Java packager executable at '${packagerFile}'")
                }
                if (!jmodFile.exists()) {
                    throw new GradleException("Unable to find Java packager module at '${jmodFile}'")
                }
            } else {
                throw new GradleException("JAVA_HOME must be set")
            }
        } else {
            if (project.hasProperty('javaPackagerPath')) {
                def packagerFile = file("${javaPackagerPath}/jpackager")

                if (!packagerFile.exists() || !packagerFile.isFile()) {
                    throw new GradleException("The Java Packager '$packagerFile' is missing")
                }
            } else {
                throw new GradleException("Use -PjavaPackagerPath=... to specify the directory containing the Java Packager")
            }
        }
    }
}

task createBundle(type: Exec, dependsOn: [processResources, checkBundlingEnvironment]) {
    description 'Build the installable bundle'
    if (operatingSystem.isWindows()) {
        commandLine 'cmd', '/c',
            bundlerScript,
            "${javafxModulePath}",
            applicationLibraryPath,
            bundlePath,
            mainJar,
            publishedVersion,
            appIcon
    } else {
        commandLine = ['sh',
            bundlerScript,
           "${-> file("${javaPackagerPath}/jpackager")}",
            packageType,
            "${javafxModulePath}",
            applicationLibraryPath,
            bundlePath,
            mainJar,
            publishedVersion,
            fileAssociations,
            appIcon,
            *extraBundlerArguments]
    }
}

createBundle.dependsOn ':core:copyDependencies', ':core:copyJavafxModules'

if (operatingSystem.isWindows()) {
    processResources {
        filesMatching('application.iss') {
            filter ReplaceTokens, tokens: [
                'bundle.version': publishedVersion,
                'executable.location': windowsExecutable.toString(),
                'bundle.content': bundleContent.toString()
            ]
        }
    }

    task completeInnoSetup(dependsOn: processResources) {
        doLast {
            copy {
                from 'icons/windows'
                into "${bundlePath}/Verisimilitude"
                include 'VerisimilitudeSession.ico'
            }
            println("Inno Setup file: ${generatedInnoSetupFile}")
        }
    }

    createBundle.finalizedBy completeInnoSetup
}